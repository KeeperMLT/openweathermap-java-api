apply plugin: "java"
apply plugin: "groovy"
apply plugin: "jacoco"
ext {
    sharedManifest = manifest {
        attributes(
                "Build-Time": buildTime,
                "Implementation-Version": version,
                "Implementation-Vendor": "Iurii Sergiichuk",
                "Specification-Version": version,
                "Specification-Vendor": "Iurii Sergiichuk"
        )
    }
}

jacoco {
    toolVersion = "0.7.6.201602180812"
}

test {
    testLogging.showStandardStreams = true
    testLogging {
        events "PASSED", "FAILED", "SKIPPED"
    }
    jacoco {
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpFile = file("$buildDir/jacoco/classpathdumps")
    }
}

check.dependsOn jacocoTestReport

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

tasks.withType(JavaCompile.class) {
    options.deprecation = false
    options.warnings = false
    options.encoding = sourceEncoding
    options.compilerArgs = ["-Xmaxerrs", "20", "-Xmaxwarns", "0", "-Xlint:all"]
    sourceCompatibility = JavaVersion.VERSION_1_7
    targetCompatibility = JavaVersion.VERSION_1_7
}

javadoc {
    options.memberLevel = JavadocMemberLevel.PROTECTED
    classpath = configurations.compile
}
//create a single Jar with all dependencies
task generateOneJar(type: Jar) {
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

tasks.withType(Jar) { // includes War and Ear
    manifest = project.manifest {
        from sharedManifest
    }
}
